;license:MIT
;(c) 2018-9 by 4am
;
; credits page
;
; Public functions
; - Credits

Credits
; clobbers all
         jsr   LoadFile              ; load credits text into $8000
         !word kRootDirectory
         !word kCreditsFile
         !word $8000
         jsr   ClearOffscreen
         lda   OffscreenPage
         ror                         ; draw on offscreen page
         +LDADDR $8000
         jsr   DrawPage              ; draw credits
         jsr   ShowOtherPage         ; show credits
         jsr   WaitForKeyFor30Seconds; wait
         bit   CLEARKBD              ; don't care about key
         jsr   ShowOtherPage         ; back to previous page
         clc                         ; if called from search mode, tell caller not to refresh
         rts

Help
; clobbers all
         jsr   LoadHelpOffscreen
         jsr   ShowOtherPage
         jsr   WaitForKeyFor30Seconds
         cmp   #$83
         beq   @exitWithoutShowingOtherPage
         jsr   IsUpDownOrRightArrow
         beq   @exitWithoutShowingOtherPage
         cmp   #$88
         beq   @exitViaBrowse
         jsr   IsSearchKey
         beq   @exitWithoutShowingOtherPage
         bit   CLEARKBD
         jsr   ShowOtherPage
@exitWithoutShowingOtherPage
         clc
         rts
@exitViaBrowse
         jmp   BrowseMode
