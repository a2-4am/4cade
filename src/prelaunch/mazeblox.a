;license:MIT
;(c) 2021-2023 by Frank M., qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/MAZEBLOX",plain
*=$106

    !source "src/macros.a"

         +ENABLE_ACCEL
         +USES_TEXT_PAGE_2
         lda   #$60
         sta   $A20
         jsr   $800       ; decompress1

         lda   #<hook2
         sta   $B747+1
         lda   #>hook2
         sta   $B747+2
         jmp   $B700      ; decompress2

hook2    lda   #<hook3
         sta   $9E4D+1
         lda   #>hook3
         sta   $9E4D+2
         ldx   #0
         stx   $9E52
         inx
         stx   $9E53      ; reset vector fix
         jmp   $9D84      ; decompress3

hook3    +GET_MACHINE_STATUS_LC
         and   #CHEATS_ENABLED
         beq   +
         lda   #<hook4
         sta   $BD07
         lda   #>hook4
         sta   $BD08
+
         +DISABLE_ACCEL_LC
         jmp   $A180

hook4
         pla
         pla
         jsr   $BD5A
         jsr   $BD09
         lda   $B7EC
         cmp   #$13
         bne   +
         lda   $B7ED
         cmp   #5
         bne   +
         lda   #$AD
         sta   $1920      ; patch - don't decrease time
+        clc
         rts

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
