;license:MIT
;(c) 2021-2022 by Frank M., qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/JELLYFISH",plain
*=$106

    !source "src/macros.a"

         +ENABLE_ACCEL

         ldx   #<reset_e
-        lda+2 $ff,x
         sta   $50,x
         dex
         bne   -

         lda   #<reset
         sta   $2049+1
         stx   $2049+2

         jmp   $2000

!pseudopc $51+<* {
reset    ldx   #5
-        lda   $51, x
         sta   $2F0, x
         dex
         bpl   -
         +RESET_VECTOR $2f0
         +READ_RAM2_NO_WRITE
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         lda   #$a5
         sta   $97BA      ; patch - don't decrease lives
+
         jsr   DisableAccelerator
         jsr   HideLaunchArtworkLC2
         +READ_ROM_NO_WRITE
         jmp   $B800
}
reset_e

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
