;license:MIT
;(c) 2021-2022 by qkumba, Frank M.

!cpu 6502
!to "build/PRELAUNCH.INDEXED/APE.ESCAPE",plain
*=$106

         !source "src/macros.a"

         +ENABLE_ACCEL_AND_HIDE_ARTWORK

         lda   #$60
         sta   $94C7
         sta   $94E1
         sta   $94F2
         jsr   $4000      ; decompress & show SAMS logo

         +DISABLE_ACCEL

         lda   $C050
         jsr   $94CA      ; show title animation

         +READ_RAM2_NO_WRITE
         jsr   EnableAccelerator
         bit   CLEARKBD
         jsr   $94E4      ; decompress game

         lda   MockingboardStuff
         bpl   no_speech
         and   #7
         ora   #$C0

         ldy   #0
-        ldx   $1300,Y    ;find #$C4s
         cpx   #$C4
         bne   +
         sta   $1300,Y    ;Replace #$C4 with detected slot
+        ldx   $6D00,Y    ;find #$C4s
         cpx   #$C4
         bne   +
         sta   $6D00,Y    ;Replace #$C4 with detected slot
+        iny
         bne   -

         sta   $7941
         sta   $7944

no_speech
         +READ_RAM2_NO_WRITE
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         lda   #$AD
         sta   $6632
         sta   $6635      ; patch - don't decrease lives
         lda   #4
         sta   $6505      ; patch - don't fall any floors after the 1st
+
         jsr   DisableAccelerator
         +READ_ROM_NO_WRITE
         jmp   $6200

!if * > HideLaunchArtwork {
  !error "code is too large, ends at ", *
}
