;license:MIT
;(c) 2022, 2024 by qkumba/Frank M.

!cpu 6502
!to "build/PRELAUNCH.INDEXED/RB",plain
*=$106

         !source "src/macros.a"

         +READ_RAM2_NO_WRITE
         jsr   HideLaunchArtworkLC2

         lda   WaitForVBL
         cmp   #$60
         beq   novbl1
         jsr   EnableAccelerator
         lda   #0
         sta   $5FFC      ; enable our VBL

novbl1
         lda   #$4C
         sta   $6127
         lda   #<callback0
         sta   $6128
         lda   #>callback0
         sta   $6129

         lda   #$4C
         sta   $612F
         lda   #<callback1
         sta   $6130
         lda   #>callback1
         sta   $6131

         jmp   $3FF8      ; decompress & show title

callback0
         lda   #0
         sta   $2000      ; remove activity indicator
-        lda   $C000      ; and wait for keypress
         bpl   -
         jmp   $8F3

callback1
         lda   WaitForVBL
         cmp   #$60
         beq   novbl2
         lda   #$4C
         sta   $14F9
         lda   #<callback2
         sta   $14FA
         lda   #>callback2
         sta   $14FB

novbl2
         lda   $C055
         jsr   DisableAccelerator
         +READ_ROM_NO_WRITE
         rts

callback2
         +READ_RAM2_NO_WRITE
         jsr   WaitForVBL
         +READ_ROM_NO_WRITE
         rts

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
