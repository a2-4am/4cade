;license:MIT
;(c) 2019-2020 by qkumba

!cpu 6502
!to "build/PRELAUNCH/BRUCE.LEE",plain
*=$106

    !source "src/prelaunch/common.a"

         +READ_RAM2_NO_WRITE
         jsr   EnableAccelerator
         lda   #$2c
         sta   $bb8
         lda   #<callback
         sta   $bb6
         lda   #>callback
         sta   $bb7
         ldx   #<cheat2
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         ldx   #<cheat1
+        stx   $bec
         lda   #>cheat1
         sta   $bed
         ldx   #cheat_e - cheat
-        lda   cheat - 1, x
         sta   <(cheat1 - 1), x
         dex
         bne   -
         bit   MachineStatus
         bvs   +
         lda   #$82
         sta   <(lcpatch + 1)
+
         +READ_ROM_NO_WRITE
         jmp   $900

callback
         +DISABLE_ACCEL
         jsr   $6000
         jsr   $7f8
         +READ_RAM2_NO_WRITE
         jsr   EnableAccelerator
         +READ_RAM1_WRITE_RAM1
         rts

cheat !pseudopc $cf {
cheat1
         lda   #$2c
         sta   $8a7e
cheat2
         lda   $c083
         jsr   DisableAccelerator
lcpatch
         lda   $c08b
         jmp   $3800
}
cheat_e

!if * > $1C0 {
  !error "code is too large, ends at ", *
}
