;license:MIT
;(c) 2020, 2022, 2023 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/BATTLE.CRUISER",plain
*=$106

    !source "src/macros.a"

         +ENABLE_ACCEL
         lda   #$60
         sta   $2027
         jsr   $2000      ; decompress

         +GET_MOCKINGBOARD_SPEECH_AND_MACHINE_STATUS_LC
         bvs   +
         stx   $868
         stx   $9486
         ldx   #$60
         stx   $B000      ; no speech chip, pretend no MB
+        and   #CHEATS_ENABLED
         beq   +
         lda   #$a5
         sta   $A22       ; patch - don't decrease lives
+
         ldy   #2
-        lda   call1, y
         sta   $8FFF, y
         lda   call2, y
         sta   $9010, y
         dey
         bpl   -
         lda   #$C5
         sta   $902A
         +NEW_RESET_VECTOR $110
         +DISABLE_ACCEL_LC
         jmp   $845

callback1
         sec
         sbc   #8
         cmp   #2
         bcc   +
-        jmp   $9014
+        ldy   #$84
         jmp   $9003

callback2
         sec
         sbc   #8
         cmp   #2
         bcs   -
         jmp   $902C

call1    jmp   callback1
call2    jmp   callback2

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
