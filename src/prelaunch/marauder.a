;license:MIT
;(c) 2021-2022 by Frank M., qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/MARAUDER",plain
*=$106

    !source "src/macros.a"

         +ENABLE_ACCEL

         lda   #$60
         sta   $864
         jsr   $800       ; decompress

         +NEW_RESET_VECTOR $3B0
         lda   #$2C
         sta   $B5D1
         sta   $B5D4
         sta   $B5D9      ; reset vector fix

         +READ_RAM2_NO_WRITE
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         lda   #<callback1
         sta   $B5F8
         lda   #<callback2
         sta   $B5B1
         sta   $B607
         lda   #>callback1
         sta   $B5F9
         sta   $B5B2
         sta   $B608
+
         jsr   DisableAccelerator
         jsr   HideLaunchArtworkLC2
         +READ_ROM_NO_WRITE

         jmp   $B5B9

callback1
         lda   #$a5
         sta   $5FC6      ; patch - don't increase lives
         sta   $6E0B      ; patch - don't decrease lives
         jmp   $6C00

callback2
         lda   #$a5
         sta   $8644      ; patch - don't decrease lives
         jmp   $800

!if * > PrelaunchMax {
  !error "code is too large for reset vector hook, ends at ", *
}
