;license:MIT
;(c) 2020, 2022 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/STARGLIDER",plain
*=$106

    !source "src/macros.a"

         +READ_RAM2_NO_WRITE
         jsr   EnableAccelerator
         lda   MachineStatus
         and   #CHEATS_ENABLED
         php
         +READ_ROM_NO_WRITE
         lda   #$60
         sta   $81FC
         jsr   $800       ; decompress

         lda   $bf30
         lsr
         lsr
         lsr
         lsr
         and   #7
         ora   #$78
         sta   patch + 1

         jsr   DisableAccelerator
         jsr   HideLaunchArtworkLC2
         jsr   $BA7B

         ldx   #8
-        lda   reset,x
         sta   $110,x
         dex
         bpl   -

         lda   #$10
         sta   $7323
         lda   #1
         sta   $732B

         plp
         beq   +
         lda   #$a5
         sta   $76B3      ; patch - don't decrease fuel
         sta   $856B      ; patch - don't increase damage
         sta   $8E13      ; patch - don't increase weapons
         sta   $9528      ; patch - don't increase damage
         sta   $9BC0      ; patch - don't increase damage
         sta   $9D34      ; patch - don't increase damage
         sta   $9D70      ; patch - don't increase damage
         sta   $9E37      ; patch - don't increase damage
+
         jmp   $BA00

reset    lda   $C089
patch    sta   $478       ; SMC, hide CFFA state in case of reset
         jmp   $FAA6

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
