;license:MIT
;(c) 2021-2022 by Frank M., qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/TITAN.CRONUS",plain
*=$106

    !source "src/prelaunch/common.a"

         +ENABLE_ACCEL
         lda   #<hook
         sta   $1053+1
         lda   #>hook
         sta   $1053+2
         jmp   $1000      ; decompress

hook     +NEW_RESET_VECTOR $280

         lda   #$8C       ; CALL
         sta   $C84
         lda   #$36       ; "640" / $280
         sta   $C84+1
         lda   #$34
         sta   $C84+2     ; return to TR instead of BASIC
         lda   #$30
         sta   $C84+3     ; (replaces TEXT:HOME:END)

         +READ_RAM2_NO_WRITE
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         lda   #<callback
         sta   $1073
         lda   #>callback
         sta   $1074
+
         jsr   DisableAccelerator
         +READ_ROM_NO_WRITE
         jmp   $D566

callback
         pla
         pla
         jsr   $1080
         jsr   $1075
         lda   #$AD
         sta   $808C      ; don't increase lives
         sta   $8595      ; don't decrease lives
         rts

!if * > $1C0 {
  !error "code is too large, ends at ", *
}
