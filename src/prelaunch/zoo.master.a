;license:MIT
;(c) 2021-2022 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/ZOO.MASTER",plain
*=$106

         !source "src/prelaunch/common.a"

         +ENABLE_ACCEL
         lda   #$60
         sta   $60A4
         jsr   $6000      ; decompress
         +RESET_VECTOR_HALF $100
         lda   #$60
         sta   $89F
         jsr   $800       ; decompress more
         +READ_RAM2_NO_WRITE
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         lda   #<callback
         sta   $48A0
         lda   #>callback
         sta   $48A1
+
         jsr   DisableAccelerator
         lda   MockingboardStuff
         +READ_ROM_NO_WRITE
         bmi   +
         jmp   $8C0

callback
         jsr   $4800
         lda   #$AD
         sta   $135E      ; patch - don't decrease lives
         rts

+
         and   #7
         ora   #$C0
         ldx   #0
-        ldy   $9A00, x
         cpy   #$C4
         bne   +
         sta   $9A00, x
+        ldy   $9B00, x
         cpy   #$C4
         bne   +
         sta   $9B00, x
+        inx
         bne   -
         jmp   $8C8

!if * > $1C0 {
  !error "code is too large, ends at ", *
}
