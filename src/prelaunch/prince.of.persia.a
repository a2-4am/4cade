;license:MIT
;(c) 2025 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/PP",plain
*=$106

    !source "src/macros.a"

         inc   $3F4       ; force reboot on reset

         +READ_RAM2_NO_WRITE
         jsr   EnableAccelerator
         jsr   HideLaunchArtworkLC2
         lda   MachineStatus
         and   #CHEATS_ENABLED
         php
         lda   WaitForVBL
         cmp   #$60
         php
         +READ_ROM_NO_WRITE
         lda   #<callback
         sta   $75A8
         lda   #>callback
         sta   $75A9
         jmp   $800       ; decompress

callback
         jsr   $D213
         pla
         pla
         plp
         bne   @good_vbl
         lda   #$60       ; if VBL disabled in TR
         sta   $c005
         sta   $4ED       ; then disable VBL in game
         sta   $c004

@good_vbl
         plp
         beq   +
         dec   $2D0E      ; patch - don't decrease time
         lda   #$a5
         sta   $c005
         sta   $2619      ; patch - don't decrease lives
         sta   $c004
+

         +READ_RAM2_NO_WRITE
         jsr   DisableAccelerator
         +READ_RAM1_WRITE_RAM1
         jmp   $EE00

!if * > HideLaunchArtwork {
  !error "code is too large, ends at ", *
}
