;license:MIT
;(c) 2025 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/PP",plain
*=$106

    !source "src/macros.a"

         inc   $3F4       ; force reboot on reset

         +ENABLE_ACCEL_AND_HIDE_ARTWORK_LC
         lda   MachineStatus
         and   #CHEATS_ENABLED
         php
         lda   WaitForVBL
         +READ_ROM_NO_WRITE
         cmp   #$60
         php
         lda   #<callback
         sta   $75A8
         lda   #>callback
         sta   $75A9
         jmp   $800       ; decompress

callback
         jsr   $D213
         pla
         pla
         plp
         bne   @good_vbl
         lda   #$60       ; if VBL disabled in TR
         sta   $c005
         sta   $4ED       ; then disable VBL in game
         sta   $c004

@good_vbl
         plp
         beq   +
         dec   $2D0E      ; patch - don't decrease time
         lda   #$a5
         sta   $c005
         sta   $2619      ; patch - don't decrease lives
         sta   $c004
+
         lda   #$60
         sta   $c005
         sta   $DE6       ; don't touch GS BRAM
         sta   $c004
         lda   $75d0
         lsr
         lsr
         lsr
         lsr
         and   #7
         tax
         sta   $478, x    ; hide CFFA state in case of reset
         +READ_RAM2_NO_WRITE
         jsr   DisableAccelerator
         +READ_RAM1_WRITE_RAM1
         jmp   $EE00

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
