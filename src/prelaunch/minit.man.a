;license:MIT
;(c) 2020 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/MINIT.MAN",plain
*=$106

    !source "src/macros.a"

         jmp   main

callback
         jsr   $BD57
         ldx   $B7EC
         ldy   $B7ED
         cpx   #$17
         bne   ++
         lda   #0
         cpy   #8
         beq   +
         cpy   #5
         bne   ++
         sta   $BB48
         rol
         sta   $BB4D      ; hook in-game reset
         bne   ++
+        sta   $BA48
         rol
         sta   $BA4D      ; hook in-game reset

++
         +GET_MACHINE_STATUS
         and   #CHEATS_ENABLED
         beq   +
         cpx   #$14
         bne   +
         cpy   #$0B
         bne   +
         lda   #$a5
         sta   $BBD2      ; patch - don't decrease lives
+        rts

main
         +ENABLE_ACCEL
         lda   #$60
         sta   $9FD
         jsr   $800       ; load DOS
         lda   #$A9
         sta   $BF24
         lda   #$01
         sta   $BF25      ; hook loader reset
         lda   #<callback
         sta   $BD26
         lda   #>callback
         sta   $BD27
+
         +DISABLE_ACCEL_AND_HIDE_ARTWORK
         jmp   $B700

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
