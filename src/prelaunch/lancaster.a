;license:MIT
;(c) 2019-2020 by qkumba

!cpu 6502
!to "build/PRELAUNCH/LANCASTER",plain
*=$106

    !source "src/prelaunch/common.a"

         lda   #<cheat
         sta   $10da
         lda   #>cheat
         sta   $10db
         jmp   $1000

cheat
         +GET_MOCKINGBOARD_SLOT_AND_MACHINE_STATUS
         and   #CHEATS_ENABLED
         beq   mbslot
                          ; lives in $0ED7
         lda   #$ad
         sta   $1661
         sta   $1476

mbslot
         bcs   +
         lda   #$60
         sta   $16A1      ; no MB, don't write to random slot
         sta   $671A      ; no MB, don't write to random slot
         bne   skipmb
+        cpx   #$C4
         beq   skipmb
         stx   $6609      ; Replace #$C4 with configured slot
         txa
         ldy   #00
-        ldx   $16AD,Y    ; Find #$C4s
         cpx   #$C4
         bne   +
         sta   $16AD,Y    ; Replace #$C4 with configured slot
+        ldx   $6720,Y    ; Find #$C4s
         cpx   #$C4
         bne   +
         sta   $6720,Y    ; Replace #$C4 with configured slot
+        iny
         bne   -

skipmb
         jmp   $1000

!if * > $1C0 {
  !error "code is too large, ends at ", *
}
