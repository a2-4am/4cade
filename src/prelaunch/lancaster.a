;license:MIT
;(c) 2019 by qkumba

!cpu 6502
!to "build/PRELAUNCH/LANCASTER",plain
*=$106

    !source "src/prelaunch/common.a"

         +GET_MACHINE_STATUS_AND_MOCKINGBOARDSLOT
         and   #CHEATS_ENABLED
         sta   cheat+1
         stx   mbslot+1
         lda   #<cheat
         sta   $10da
         lda   #>cheat
         sta   $10db
         jmp   $1000

cheat
         lda   #0         ; SMC (will be non-zero if cheats are enabled)
         beq   +
                          ; lives in $0ED7
         lda   #$ad
         sta   $1661
         sta   $1476
+

mbslot
         ldx #0           ; SMC (will be non-zero if mockingboard slot is valid)
         beq skipmb
         stx $6609        ;Replace #$C4 with configured slot
         ldy #$20         ;Start scan at $6720
-
         lda $6700,Y      ;Find #$C4's (21 locations) in code
         cmp #$C4
         bne +
         txa
         sta $6700,Y      ;Replace #$C4 with configured slot
+
         iny              ;Finish scan at $67FF
         bne -
skipmb
         jmp   $1000

!if * > $1C0 {
  !error "code is too large, ends at ", *
}
