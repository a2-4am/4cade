;license:MIT
;(c) 2019-2020 by qkumba/Tom Greene

!cpu 6502
!to "build/PRELAUNCH.INDEXED/BW",plain
*=$106

    !source "src/macros.a"

         jmp   main

brunpatch
         lda   $AA76      ; check second character of filename
         cmp   #$C9       ; = I, filename = @INIT
         bne   +

         ldx   #$00       ; Patch it to return to TR
         stx   $0DB4      ; instead of exiting to BASIC
         inx
         stx   $0DB5

         inc   $0C85      ; prevent language card detection

+        cmp   #$C6       ; = F, filename = @FHR
         bne   ++

         +GET_MACHINE_STATUS
         and   #CHEATS_ENABLED
         beq   ++
         ldx   #$2C
         stx   $15AE      ; don't decrement bullets

++       jmp   ($AA72)    ; finally jump to the loaded file
patch
         !word brunpatch

main
         +ENABLE_ACCEL
         +USES_TEXT_PAGE_2
         lda   #$60
         sta   $97C
         jsr   $800       ; find and load "DOS"
         lda   #<callback
         sta   $B748
         lda   #>callback
         sta   $B749
         jmp   $B700

callback
         lda   #$01
         sta   $9E31
         lda   #$00
         sta   $9E3C      ; reset vector patch

         lda   #<patch    ; intercept BRUN command before
         sta   $A395      ; it jumps to the loaded file
         lda   #>patch
         sta   $A396

         +DISABLE_ACCEL_AND_HIDE_ARTWORK
         jmp   $9D84


!if * > HideLaunchArtwork {
  !error "code is too large, ends at ", *
}
