;license:MIT
;(c) 2020, 2024 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/FROGGER.II",plain
*=$106

    !source "src/macros.a"

         +ENABLE_ACCEL
         lda   #$60
         sta   $6068
         jsr   $6000      ; decompress
         +NEW_RESET_VECTOR $280
         ldx   #(callback_e - callback) - 1
-        lda   callback, x
         sta   $200, x    ; stack leak overwrites prelaunch eventually
         dex
         bpl   -
         lda   #$4C
         sta   $B04A
         inx
         stx   $B04B
         lda   #2
         sta   $B04C
         +DISABLE_ACCEL_AND_HIDE_ARTWORK
         jmp   $800

callback !pseudopc $200 {
         +ENABLE_ACCEL
         jsr   $B050
         +GET_MACHINE_STATUS
         and   #CHEATS_ENABLED
         beq   ++
         ldx   #$A5
         lda   $B158
         cmp   #$30
         bne   +
         stx   $A1C8      ; patch - don't decrease lives
         bne   ++
+        cmp   #$31
         bne   +
         lda   #$BD
         sta   $FB3       ; patch - don't decrease lives
         bne   ++
+        stx   $9A35      ; patch - don't decrease lives
++
         ldx   #(patch_e - patch_b) - 1
-        lda   patch_b, x
         sta   $9AD4, x
         dex
         bpl   -
         +DISABLE_ACCEL
         jmp   ($B04E)

patch_b
         ldx   #0
         jsr   $FB1E
         tya
         pha
         inx
         jsr   $FB1E
         pla
         tax
         rts
patch_e
}
callback_e


!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
