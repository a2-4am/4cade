;license:MIT
;(c) 2024-2025 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/WILLY.BYTE",plain
*=$106

patch_iic=$d050 ; from willy.byte.mb.a
accel=$d1fc     ; from willy.byte.mb.a
accel_e=$d218   ; from willy.byte.mb.a
callback1=$bf80 ; from willy.byte.mb.a

    !source "src/macros.a"

         ldx   $FBB3
         cpx   #6
         bne   not_iic1
         ldx   $FBC0

not_iic1
         php
         +READ_RAM2_WRITE_RAM2
         jsr   EnableAccelerator
         jsr   HideLaunchArtworkLC2
         +LOAD_FILE_AT willybyte_mb, $D000
         plp
         bne   not_iic2
         lda   #$2c
         sta   patch_iic + 1

not_iic2
         ldx   #(accel_e - accel)
-        lda   accel, x
         sta   callback1, x
         dex
         bpl   -
         +READ_ROM_NO_WRITE
         lda   #<callback1
         sta   $414A
         lda   #>callback1
         sta   $414B
         lda   #$60
         sta   $426B
         jsr   $4000      ; decompress
         lda   #$20
         sta   $7852
         lda   #<callback2
         sta   $7853
         lda   #>callback2
         sta   $7854
         lda   #$EA
         sta   $7855
         lda   #$4C
         sta   $B6D8
         ldx   #0
         stx   $B6D9
         inx
         stx   $B6DA      ; reset vector
         jmp   $6000

callback2
         sbc   $8A
         cmp   #8
         beq   +
         cmp   #9
+        rts

willybyte_mb
         +PSTRING "PRELAUNCH/WILLY.BYTE.MB"

!if * > $1bf {
  !error "code is too large, ends at ", *
}
