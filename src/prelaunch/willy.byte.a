;license:MIT
;(c) 2024-2026 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/WILLY.BYTE",plain
*=$106

patch_iic=$d04b ; from willy.byte.mb.a
callback4=$bf95 ; from willy.byte.mb.a
patch_val=$bfb5 ; from willy.byte.mb.a
accel=$d156     ; from willy.byte.mb.a
accel_e=$d199   ; from willy.byte.mb.a
callback1=$bf80 ; from willy.byte.mb.a

    !source "src/macros.a"

         ldx   $FBB3
         cpx   #6
         bne   not_iic1
         ldx   $FBC0

not_iic1
         php
         +READ_RAM2_WRITE_RAM2
         jsr   EnableAccelerator
         jsr   HideLaunchArtworkLC2
         +LOAD_FILE_AT willybyte_mb, $D000
         plp
         php
         bne   not_iic2
         lda   #$2c
         sta   patch_iic + 1

not_iic2
         ldx   #(accel_e - accel) - 1
-        lda   accel, x
         sta   callback1, x
         dex
         bpl   -
         lda   $D601
         sta   patch_val + 1
         +READ_ROM_NO_WRITE
!if <callback1 != $80 {
         lda   #<callback1
         sta   $414A
}
         lda   #>callback1
         sta   $414B
         lda   #$60
         sta   $426B
         jsr   $4000      ; decompress
         ldx   #3
-        lda   patch_c2 - 1, x
         sta   $7852 - 1, x
         stx   $B6DA      ; reset vector
         dex
         bne   -
         lda   #$EA
         sta   $7855
         lda   #$4C
         sta   $B6D8
         stx   $B6D9
         plp
         bne   not_iic3
         ldx   #3
-        lda   patch_c4 - 1, x
         sta   $BD00 - 1, x
         dex
         bne   -

not_iic3
         jmp   $6000

callback2
         sbc   $8A
         cmp   #8
         beq   +
         cmp   #9
+        rts

patch_c2 !byte $20, <callback2, >callback2
patch_c4 !byte $20, <callback4, >callback4

willybyte_mb
         +PSTRING "PRELAUNCH/WILLY.BYTE.MB"

!if * > $1bf {
  !error "code is too large, ends at ", *
}
