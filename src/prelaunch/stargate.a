;license:MIT
;(c) 2021-2022 by Frank M., qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/STARGATE",plain
*=$106

    !source "src/macros.a"

         +ENABLE_ACCEL

         lda   #$60
         sta   $4697
         jsr   $800       ; decompress

         lda   #$60
         sta   $9A03
         jsr   $9A00      ; decompress2

         lda   #0
         sta   $47F2
         sta   $47FE
         lda   #1
         sta   $47F3
         sta   $47FF      ; IRQ vector (moved to $3FE/F)
         lda   #$A4
         sta   $47F4      ; reset vector (moved to $3F2/3/4)

         +READ_RAM2_NO_WRITE
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         ldx   #$FF
         stx   $1946      ; patch - don't decrease people
         inx
         stx   $1C11      ; patch - don't decrease lives
         lda   #$25
         sta   $1E98      ; patch - don't decrease bombs
+
         jsr   DisableAccelerator
         jsr   HideLaunchArtworkLC2
         +READ_ROM_NO_WRITE
         jmp   $5000

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
