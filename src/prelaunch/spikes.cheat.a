;license:MIT
;(c) 2020, 2022 by qkumba

!cpu 6502
!to "build/PRELAUNCH/SPIKES.CHEAT",plain
*=$250

         !source "src/constants.a"   ; no code in these
         !source "src/macros.a"

         jsr   EnableAccelerator
         +READ_ROM_NO_WRITE
         lda   #$60
         sta   $99A
         sta   $A15
         sta   $A89
         sta   $BE3
         jsr   $900        ; decompress
         lda   #<callback3
         sta   $8B1
         lda   #<callback1
         sta   $9501
         lda   #<callback2
         sta   $95B1
         lda   #>callback1
         sta   $8B2
         sta   $9502
         sta   $95B2
         jmp   $9500

callback1
         +ENABLE_ACCEL
         jsr   $9503
         +DISABLE_ACCEL
         jsr   $BC6C
         sta   CLEARKBD

callback2
         +READ_RAM2_NO_WRITE
         jsr   EnableAccelerator
         jsr   $BC7B

         +GET_MACHINE_STATUS_LC
         and   #CHEATS_ENABLED
         beq   +
         lda   #$ad
         sta   $6AB0       ; patch - don't decrease fuel
         sta   $6B5C       ; patch - don't decrease lives
+
         +DISABLE_ACCEL_LC
         lda   #$30
         jsr   $FCA8       ; allow joystick timers to expire
         jmp   $A00

callback3
         +READ_RAM2_NO_WRITE
         jsr   EnableAccelerator
         jsr   $BE01

         +GET_MACHINE_STATUS_LC
         and   #CHEATS_ENABLED
         beq   +
         lda   #$ad
         sta   $6D9B       ; patch - don't decrease fuel
         sta   $7EA0       ; patch - don't decrease lives
         sta   $8AF1       ; patch - don't decrease lives
+
         +DISABLE_ACCEL_LC
         jmp   $8D00
