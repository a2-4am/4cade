;license:MIT
;(c) 2019 by 4am

!cpu 6502
!to "build/PRELAUNCH/XEVIOUS",plain
*=$106

         !source "src/prelaunch/common.a"

         +ENABLE_ACCEL

         +READ_RAM2_WRITE_RAM2
         lda   #<reset
         sta   $FFFC
         sta   $3F2       ; $3F3/3F4 already set to page 1
         lda   #>reset
         sta   $FFFD      ; reset vector patch

         +GET_MACHINE_STATUS
         and   #CHEATS_ENABLED
         sta   patch2+1
         lda   #<patch1
         sta   $35E4
         lda   #>patch1
         sta   $35E5
         jmp   $34AA      ; decompress 1

reset
         +READ_ROM_NO_WRITE
         jmp   $FAA6
         
patch1
         +DISABLE_ACCEL
         lda   #<patch2
         sta   $ACE8
         lda   #>patch2
         sta   $ACE9
         jmp   $AC48      ; decompress 2/first title
patch2
         lda   #0         ; SMC
         beq   +
                          ; lives in $602B
         lda   #$AD
         sta   $6414      ; patch - don't decrease lives
         sta   $6A8C      ; patch - don't increase lives
         sta   $750D      ; patch - don't increase lives
         sta   $894F      ; patch - don't increase lives
+
         jmp   $0600      ; play demo/start game

!if * > $1C0 {
  !error "code is too large, ends at ", *
}
