;license:MIT
;(c) 2020 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/SUPER.ZAXXON",plain
*=$106

    !source "src/macros.a"

         +READ_RAM2_WRITE_RAM2
         +LC_REBOOT
         lda   MachineStatus
         +READ_ROM_NO_WRITE
         and   #CHEATS_ENABLED
         beq   +
         lda   #0
         sta   $21C2
         lda   #$71
         sta   $21C3
         ldy   #(callback_e - callback) - 1
-        lda   callback, y
         sta   $7100, y
         dey
         bpl   -
+
         ldx   #(hide_callback_e-hide_callback)
-        lda   hide_callback, x
         sta   $106, x
         lda   PrelaunchMax, x
         sta   HideArtworkCallback, x
         dex
         bpl   -
         lda   #$06
         sta   $21CE
         lda   #$01
         sta   $21CF
         jmp   $2000      ; decompress

callback !pseudopc $7100 {
         jsr   $415
         lda   #$bd
         sta   $1999      ; patch - don't decrease lives
         rts
}
callback_e

hide_callback !pseudopc $106 {
         ldx   #$0D
         jsr   $193
         jsr   HideArtworkCallback
         jmp   $56C4
HideArtworkCallback
}
hide_callback_e

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
