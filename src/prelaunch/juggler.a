;license:MIT
;(c) 2019, 2022, 2025 by Frank M., qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/JUGGLER",plain
*=$106

    !source "src/macros.a"

         +ENABLE_ACCEL

         ldy   #6
-        lda   $100-1,y
         sta   $180-1,y   ; move hook to $180
         dey
         bne   -
         lda   #$80
         sta   $3F2       ; reset vector patch

         lda   #$60
         sta   $7D1F
         jsr   HideLaunchArtwork
         jsr   $3FF5      ; unpack & show title

         lda   #$60
         sta   $BA21
         jsr   $BA00

         +READ_RAM2_NO_WRITE
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         lda   #$a5
         sta   $9475      ; patch - don't decrease lives
+
         jsr   DisableAccelerator
         +READ_ROM_NO_WRITE
         jmp   $6000

!if * > HideLaunchArtwork {
  !error "code is too large, ends at ", *
}
