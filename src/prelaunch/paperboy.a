;license:MIT
;(c) 2019-2020, 2022 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/PAPERBOY",plain
*=$106

    !source "src/macros.a"

         +READ_RAM2_NO_WRITE
         jsr   EnableAccelerator
         jsr   HideLaunchArtworkLC2
         lda   MachineStatus
         and   #CHEATS_ENABLED
         sta   callback_b+(cheat_smc-callback)+1
         +READ_ROM_NO_WRITE
         lda   #$60
         sta   $8E1
         inc   $bf0e      ; disable ROM mapping
         jsr   $800
         lda   #$20
         sta   $8E1
         jsr   DisableAccelerator
         lda   #<callback
         sta   $20F
         lda   #>callback
         sta   $210
         ldx   #callback_e-callback_b
-        lda   callback_b,x
         sta   callback,x
         dex
         bpl   -
         jmp   $8E1
callback_b !pseudopc $BF80 {
callback
         sta   $20F
         lda   #$60
         sta   $210
cheat_smc
         lda   #0
         beq   +
         lda   #$ad
         sta   $8277      ; patch - don't decrease lives
+        jmp   $6000
}
callback_e

!if * > HideLaunchArtwork {
  !error "code is too large, ends at ", *
}
