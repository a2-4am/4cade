;license:MIT
;(c) 2020 by qkumba/Frank M.

!cpu 6502
!to "build/PRELAUNCH.INDEXED/G.I.JOE",plain
*=$106

    !source "src/macros.a"

         +ENABLE_ACCEL
         lda   #$60
         sta   $99f
         jsr   $800       ; decompress

         +READ_RAM1_WRITE_RAM1
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         lda   #<callback1
         sta   $D45C
         lda   #<callback2
         sta   $D5A2
         lda   #>callback1
         sta   $D45D
         sta   $D5A3
+
         lda   #$2C       ; vehicle level overwrites $100-102 explicitly
         sta   $110
         lda   #$89
         sta   $111
         lda   #$C0
         sta   $112
         lda   #$4C
         sta   $113
         lda   #$A6
         sta   $114
         lda   #$FA
         sta   $115       ; update reset hook to reboot
         inc   $3F4
         lda   #1
         sta   $FFFD
         lda   #$10
         sta   $FFFC      ; LC reset vector fix

         +READ_RAM2_WRITE_RAM2
         jsr   DisableAccelerator
         jsr   HideLaunchArtworkLC2
         +READ_RAM1_WRITE_RAM1
         jmp   $D400

callback1
         lda   #$AD
         sta   $63FA      ; patch - don't increase hits
         jmp   $6000

callback2
         lda   #$AD
         sta   $14B5      ; patch - don't decrease lives
         jmp   $1100

!if * > HideLaunchArtwork {
  !error "code is too large, ends at ", *
}
