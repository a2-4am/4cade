;license:MIT
;(c) 2021-2022 by Frank M., qkumba

!cpu 6502
!to "build/DEMO/BEER.RUN#060300",plain
*=$300

         !source "src/constants.a"   ; no code in these
         !source "src/macros.a"

         +READ_RAM2_WRITE_RAM2
         jsr   EnableAccelerator
         +LOAD_FILE_KEEP_DIR beer_run, beer_run_dir_e-beer_run_dir_b
         lda   #$60
         sta   $2065
         jsr   $2000      ; decompress1

         lda   #$60
         sta   $BB2C
         jsr   $BB00      ; decompress2

         ldx   #3-1
-        lda   patch,x
         sta   $419C,x
         sta   $6C62,x
         sta   $4085,x    ; set exits
         lda   patch2,x
         sta   $4088,x    ; decrement custom counter after demo cycle and eventually exit
         dex
         bpl   -

         lda   #$D0
         sta   $67D1      ; disable sound

         +DISABLE_ACCEL

         jmp   $4000

count    !byte $03
         dec   count
         bmi   patch
         jmp   $40C8

patch    !byte $4C,$00,$01
patch2   !byte $4C,<(count+1),>(count+1)

beer_run
         !byte beer_run_e-beer_run_b
beer_run_b
beer_run_dir_b
         !text "X/BEER.RUN"
beer_run_dir_e
         !text "/BEER.RUN"
beer_run_e

!if * > $3F0 {
  !error "code is too large, ends at ", *
}
