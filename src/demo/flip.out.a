;license:MIT
;(c) 2021-2022 by Frank M., qkumba

!cpu 6502
!to "res/DEMO/FLIP.OUT",plain
*=$300

         !source "src/constants.a"   ; no code in these
         !source "src/macros.a"

         +ENABLE_ACCEL_LC
         +LOAD_XSINGLE title

         lda   #$60
         sta   $3EEE
         jsr   $3EC7      ; decompress

         lda   #$60
         sta   $BF0E
         jsr   $BEF7      ; decompress and show title

         ldx   #13-1
-        lda   patch1, x
         sta   $8B39, x
         dex
         bpl   -          ; exit on keypress on main menu

         ldx   #7-1
-        lda   patch2, x
         sta   $88BF, x
         dex
         bpl   -

         lda   #$10
         sta   $6474
         sta   $685E
         lda   #$1C
         sta   $6474+1
         sta   $685E+1

         ldx   #3-1
-        lda   patch3,x
         sta   $6474+2,x
         sta   $685E+2,x
         sta   $78CD,x    ; set exits
         dex
         bpl   -

         lda   #$D0
         sta   $7716
         sta   $7730      ; disable sound

         +DISABLE_ACCEL

         jmp   $8F63

title    !byte x_e-x_b
x_b      !text "FLIP.OUT"
x_e

patch1   !byte $B0,$08,$AD,$00,$C0,$10,$1F,$4C,$00,$01,$20,$69,$8B
patch2   !byte $10,$03
patch3   !byte $4C,$00,$01,$EA,$EA

!if * > $3F0 {
  !error "code is too large, ends at ", *
}
