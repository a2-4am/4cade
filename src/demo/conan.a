;license:MIT
;(c) 2022 by qkumba/Frank M.

!cpu 6502
!to "build/DEMO/CONAN#060200",plain
*=$200

         !source "src/constants.a"   ; no code in these
         !source "src/macros.a"

         +READ_RAM2_WRITE_RAM2
         jsr   EnableAccelerator
         +LOAD_FILE_KEEP_DIR conan, conan_dir_e-conan_dir_b
         lda   #$60
         sta   $81E
         jsr   $800       ; decompress
         lda   #$60
         sta   $BD40
         jsr   $BCCF

         lda   #0        ; vector fixes
         sta   $7C09     ; to $3FE - IRQ
         lda   #1
         sta   $7C01     ; to $3F3 - reset
         sta   $7C0B     ; to $3FF - IRQ
         lda   #$A4
         sta   $7C02     ; to $3F4 - reset ($3F2 doesn't change)

         lda   #$4C
         sta   $7C2C
         jsr   $5FF8      ; decompress, run intro
         sta   $80F6
         jsr   $826
         lda   #$60
         sta   $6BA2
         jsr   $3FFC      ; decompress
         jsr   $BE00      ; decompress more
         lda   #$60
         sta   $1094
         lda   #$6C
         sta   $BD40
         jsr   $6BA5
         ldx   #3-1
-        lda   patch,x
         sta   $A4E2,x
         sta   $A4D3,x
         sta   $A0D4,x
         dex
         bpl   -
         lda   $C057
         lda   $C052
         lda   $C050
         +DISABLE_ACCEL
         jmp   $A000

patch    !byte $4C,$00,$01

conan
         !byte conan_e-conan_b
conan_b
conan_dir_b
         !text "X"
conan_dir_e
         !text "/CONAN"
conan_e
