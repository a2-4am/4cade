;license:MIT
;(c) 2018-9 by 4am
;
; GR action slideshows
;
; Public functions
; - GRActionSlideshow
; - GRSingle
;

GRActionSlideshow
         jsr   .LoadGRTransition     ; load transition effect code at $6000
         jsr   BlankGR               ; switch GR mode with initial blank screen
         jsr   okvs_iter             ; cycle through all listed GR files
         !word gSlideshowStore
         !word .GRActionCallback     ; address of callback (called on each file)
         jmp   BlankHGR              ; switch back to HGR mode with initial blank screen on exit

GRSingle
         +STAY @fname
         jsr   BlankGR               ; switch to GR mode with initial blank screen
         jsr   LoadFile              ; load GR screenshot into $6000
         !word kRootDirectory
@fname   !word $FDFD                 ; SMC
         !word $6000
         jsr   .LoadGRTransition     ; load transition effect code at $6400
         +LDADDR $6400
         jsr   ExecuteTransitionAndWait
         jmp   BlankHGR              ; switch back to HGR mode with initial blank screen on exit

;------------------------------------------------------------------------------
; BlankGR
; clear and show GR page 1 without flickering
;
; in:    none
; out:   text page clobbered
;------------------------------------------------------------------------------
BlankGR
         ldx   #$04
         stx   @loop+2
         lda   #0
         tay
@loop    sta   $FD00,y
         iny
         bne   @loop
         inc   @loop+2
         dex
         bne   @loop
         bit   PAGE1
         bit   $C056                 ; set GR mode
         bit   $C052
         bit   $C050
         rts

;------------------------------------------------------------------------------
; .LoadGRTransition [private]
;
; in:    none
; out:   all registers and flags clobbered
;        $6400..$BFFF/main contains transition effect code (probably not all
;        of that, but no promises)
;------------------------------------------------------------------------------
.LoadGRTransition
         jsr   LoadFile
         !word kFXDirectory
         !word kGRFizzleFile
         !word $6400
GRRTS    rts

;------------------------------------------------------------------------------
; .GRActionCallback [private]
; callback called by okvs_iter on gSlideshowStore
; to load and display a single GR action screenshot

; in:    A/Y contains address of filename (name only, path is always /ACTION.GR/)
;        X contains 0-based index of the current record in gSlideshowStore
;        gGamesListStore must be initialized
; out:   all registers and flags clobbered
;        $0800..$1EFF preserved (this contains the gSlideshowStore OKVS data)
;        $2000..$BFFF clobbered by graphics data and transition code
;------------------------------------------------------------------------------
.GRActionCallback
         bit   KBD
         bmi   GRRTS

         +STAY +

         jsr   GetGameInfoInActionSlideshow
         ; if game name is not found (C will be set here), it means the game
         ; can't be played due to memory or joystick requirements, so we hide
         ; it from slideshows
         bcs   GRRTS
         stx   gGameToLaunch

         jsr   LoadFile              ; load GR screenshot into $6000
         !word kGRActionDirectory
+        !word $FDFD                 ; SMC
         !word $6000

         +LDADDR $6400
         jmp   ExecuteTransitionAndWait
