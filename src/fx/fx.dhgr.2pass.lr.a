;license:MIT
;(c) 2018 by 4am
;
!cpu 6502
!to "build/FX.INDEXED/DHGR.TWOPASS.LR",plain
*=$6000

;hgrlo    =     $0201                 ; [$C0 bytes, main memory only] defined in constants.a
;hgr1hi   =     $0301                 ; [$C0 bytes, main memory only] defined in constants.a

         !source "src/fx/macros.a"

         +COPY_TO_0 start, end
         jsr   iBuildHGRTables
         jsr   iReadyToPaint
         jmp   loop

start
!pseudopc 0 {
loop     ldy   #$00
         inc   <startx
columnloop
startx=*+1
         ldx   #$FF
rowloop
         lda   hgrlo, x
         sta   <src+1
         sta   <dst+1
         lda   hgr1hi, x
         sta   <dst+2
         eor   #$60
         sta   <src+2
         clc
         !byte $A9
-        sec
src      lda   $FDFD, y              ; SMC
dst      sta   $FDFD ,y              ; SMC
         sta   READAUXMEM
         sta   WRITEAUXMEM
         bcc   -
         sta   READMAINMEM
         sta   WRITEMAINMEM
         inx
         inx
         cpx   #$C0
         bcc   rowloop
         lda   #$10
         jsr   iWaitForKeyWithTimeout
         bmi   exit
         iny
         cpy   #$28
         bne   columnloop
         lda   <startx
         beq   loop
exit     rts
}
end
