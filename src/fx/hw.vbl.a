;license:MIT
;(c) 2019 by 4am
;
; Public functions:
; - WaitForVBL
;

         !source "src/fx/macros.a"
         !source "src/constants.a"

WaitForVBL
         bit   $FDFD      ; SMC

         pha
         tya
         pha
         +READ_ROM_NO_WRITE
         lda   $FBB3
         cmp   #$06
         bne   @nop
         lda   $FBC0
         beq   @iic
         +LDADDR WaitForVBL_iie
         bne   +
@iic
         lda   #$EA
         sta   UnwaitForVBL
         +LDADDR WaitForVBL_iic
         bne   +
@nop
         +LDADDR WaitForVBL_nop
         bne   +          ; always branches

+
         +STAY WaitForVBL+1
         lda   #$4C       ; JMP opcode
         sta   WaitForVBL

         +READ_RAM1_WRITE_RAM1
         pla
         tay
         pla
         jmp   WaitForVBL

WaitForVBL_nop            ; II/II+ have no easy VBL
         rts

WaitForVBL_iie            ; IIe/IIgs have a relatively sane VBL
-        bit   $c019
         bpl   -
-        bit   $c019
         bmi   -
         rts

WaitForVBL_iic            ; IIc is special
         sei
         sta   $C07F      ; enable access to VBL register
         sta   $C05B      ; enable VBL polling
         sta   $C07E      ; disable access to VBL register
-        bit   $C019
         bpl   -
         lda   $C070      ; $c019 bit 7 is sticky, reset it
         rts

UnwaitForVBL
         rts              ; SMC on IIc
-        bit   $C019
         bpl   -
         lda   $C070      ; $c019 bit 7 is sticky, reset it
         sta   $C07F      ; enable access to VBL register
         sta   $C05A      ; disable VBL polling
         sta   $C07E      ; disable access to VBL register
         rts
