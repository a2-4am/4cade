;license:MIT
;(c) 2021 by 4am

!cpu 6502
!to "build/FX.INDEXED/SHR.TWOPASS.LR",plain
*=$A000

shrlo = $301                         ; $C8 bytes, indexed as shrlo-1,x
shrhi = $38                          ; $C8 bytes, indexed as shrhi-1,x

         !source "src/fx/macros.a"

         +COPY_TO_0 start, end
         +BUILD_SHR_LOOKUP_TABLES shrlo, shrhi
         ;X=0
         +COPY_SCB_AND_PALETTES
         ;WRITEAUXMEM active
         jsr   iReadyToPaint
         jmp   loop1

start
!pseudopc 0 {
loop1    ldy   #0
loop2    ldx   #1
loop3    lda   shrlo, x
         sta   <dst+1
         lda   shrhi, x
         sta   <dst+2
         lda   (<dst+1), y
dst      sta   $0000, y              ; SMC
         inx
         inx
         cpx   #$C8
         bcc   loop3
         lda   KBD
         bmi   @exit
         iny
         cpy   #$A0
         bne   loop2
         dec   <loop2+1
         bpl   loop1
@exit
         rts
}
end

; if zero page code gets too large, move shrhi to $201
!if end-start > $38 {
    !error "code is too large: ends at ", end-start
}
