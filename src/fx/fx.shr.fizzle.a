;license:MIT
;(c) 2019-2020 by qkumba/4am/John Brooks

!cpu 6502
!to "build/FX/SHR.FIZZLE",plain
*=$A000

addrs=$80                            ; [128 bytes, can be anywhere in main memory but don't cross page boundary]

         ldx   #(end-start)          ; copy LFSR code to zero page
-        lda   start-1, x
         sta   $0, x
         dex
         bne   -
         ldy   #$80                  ; create address lookup table
         lda   #$9F
         sec
-        sta   addrs-1, y
         sbc   #1
         dey
         bne   -
         sta   $C005
-        lda   $9D00, y              ; pre-copy SHR SCB and palette
         sta   $9D00, y
         lda   $9E00, y
         sta   $9E00, y
         lda   $9F00, y
         sta   $9F00, y
         iny
         bne   -
         jmp   loop                  ; exit via LFSR code on zero page

start
!pseudopc 1 {
; in: X,Y=0
loop     txa
loop1    eor   #$60                  ; LFSR form 0x6000 with period 32767
         tax
loop2    lda   addrs, x
         sta   <dst+2
         lda   (<dst+1), y
dst      sta   $FD00, y
         txa
         lsr
         tax
         tya
         ror
         tay
         bcc   loop2
         bne   loop
         bit   $C000
         bmi   exit
         txa
         bne   loop1
exit     lda   $2000                 ; last lousy byte (because LFSR never hits 0)
         sta   $2000
         sta   $C004
         rts
}
end
