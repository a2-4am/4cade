;license:MIT
;(c) 2020 by 4am
;
!cpu 6502
!to "build/FX.INDEXED/DHGR.BAR.DISSLV",plain
*=$6000

;hgrlo    =     $0201                 ; [$C0 bytes, main memory only] defined in constants.a
;hgr1hi   =     $0301                 ; [$C0 bytes, main memory only] defined in constants.a

         !source "src/fx/macros.a"

         +COPY_TO_0 start, end
         ;X=0
         jsr   iBuildHGRTables
         jsr   iReadyToPaint
         jmp   loop

start
!pseudopc 0 {
row1=*+1
loop     ldx   #$40
         lda   hgrlo-$40, x
         sta   <src1+1
         sta   <dst1+1
         lda   hgr1hi-$40, x
         sta   <dst1+2
         eor   #$60
         sta   <src1+2
         clc
         +HIDE_NEXT_BYTE
-        sec
         ldy   #$27
src1     lda   $FDFD, y              ; SMC
dst1     sta   $FDFD, y              ; SMC
         dey
         dey
         bpl   src1
         jsr   toaux
         bcc   -
         jsr   tomain

row2=*+1
         ldx   #$BF
         lda   hgrlo, x
         sta   <src2+1
         sta   <dst2+1
         lda   hgr1hi, x
         sta   <dst2+2
         eor   #$60
         sta   <src2+2
         clc
         +HIDE_NEXT_BYTE
-        sec
         ldy   #$26
src2     lda   $FDFD, y              ; SMC
dst2     sta   $FDFD, y              ; SMC
         dey
         dey
         bpl   src2
         jsr   toaux
         bcc   -
         jsr   tomain
         lda   #$30
         jsr   iWaitForKeyWithTimeout
         bmi   exit
         dec   row2
         inc   row1
         bne   loop
tomain   sta   READMAINMEM
         sta   WRITEMAINMEM
exit     rts
toaux    sta   READAUXMEM
         sta   WRITEAUXMEM
         rts
}
end
