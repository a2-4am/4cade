;license:MIT
;(c) 2018 by 4am
;
!cpu 6502
!to "build/FX.INDEXED/INTERLOCK.UD",plain
*=$6000

row1 = $fe
row2 = $ff

         !source "src/fx/macros.a"

         lda   #$00
         sta   row1
         lda   #$5F
         sta   row2
         jsr   iReadyToPaint
@loop    lda   row1
         asl
         jsr   HGRCalc
         ldy   #$27
-        lda   ($3c),y
         sta   ($26),y
         dey
         bpl   -
         lda   row2
         asl
         clc
         adc   #$01
         jsr   HGRCalc
         ldy   #$27
-        lda   ($3c),y
         sta   ($26),y
         dey
         bpl   -
         lda   #$30
         jsr   iWaitForKeyWithTimeout
         bmi   @exit
         inc   row1
         dec   row2
         bpl   @loop
@exit    rts

         +HGR_CALC_ROUTINES
