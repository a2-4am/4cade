;license:MIT
;(c) 2018 by 4am
;
!cpu 6502
!to "build/FX.INDEXED/INTERLOCK.LR",plain
*=$6000

row = $FF

         !source "src/fx/macros.a"

         lda   #$00
         sta   @h1
         lda   #$27
         sta   @h2
         jsr   iReadyToPaint
@outerloop
         lda   #$BF
         sta   row
@loop    lda   row
         jsr   HGRCalc
@h1=*+1
         ldy   #$00
         lda   ($3c),y
         sta   ($26),y
         dec   row
         lda   row
         jsr   HGRCalc
@h2=*+1
         ldy   #$27
         lda   ($3c),y
         sta   ($26),y
         dec   row
         lda   row
         cmp   #$FF
         bne   @loop
         lda   #$20
         jsr   iWaitForKeyWithTimeout
         bmi   @exit
         inc   @h1
         dec   @h2
         bpl   @outerloop
@exit    rts

         +HGR_CALC_ROUTINES
