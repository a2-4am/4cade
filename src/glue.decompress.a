;license:MIT
;(c) 2018-2020 by 4am & qkumba
;
; Exomizer glue functions
;
; Public functions
; - DecompressHGR
; - DecompressDHGR
; - DecompressSHR
;

; these are maintained by hand and must remain in sync with
; labels in exodecrunch.a, which is assembled separately
DECRUNCH_EXIT_ON_KEYPRESS = $025c    ; set to 'RTS' to exit decrunch on keypress
DECRUNCH = $031a                     ; decrunch entry point

;------------------------------------------------------------------------------
; DecompressSHR
;
; in:    compressed SHR loaded to $1FF8+/main
;        file size in sizelo2/sizehi2 as set by ProRWTS2
; out:   decompressed SHR at $2000..$9FFF/main
;        $00A7..$00A8 clobbered by decompressor
;        $00AE..$00AF clobbered by decompressor
;        $00FC..$00FF clobbered by decompressor
;        $0200..$03CB clobbered by decompressor
;------------------------------------------------------------------------------
DecompressAuxSHR
         bit   SHRRTS
         !byte $A9
DecompressSHR
         clv
         lda   #$1F
         bne   DecompressInternal    ; alwyas branches

;------------------------------------------------------------------------------
; DecompressHGR
;
; in:    compressed HGR loaded to $3FF8+/main
;        file size in sizelo2/sizehi2 as set by ProRWTS2
; out:   decompressed HGR at $4000..$5FFF/main
;        $00A7..$00A8 clobbered by decompressor
;        $00AE..$00AF clobbered by decompressor
;        $00FC..$00FF clobbered by decompressor
;        $0200..$03CB clobbered by decompressor
;------------------------------------------------------------------------------
DecompressHGR
         lda   #$3F
         clv
         ; execution falls through here

DecompressInternal
         pha
         php
         jsr   SwitchToBank2
         jsr   EnableAccelerator
         plp
         ldx   #0
         bvc   +
         inx
+        stx   auxreq
         lda   sizehi2
         pha
         lda   sizelo2
         pha
         lda   #>(@params - 1)
         pha
         lda   #<(@params - 1)
         pha
         bvs   +
         jmp   LoadIndexedFile       ; load decompression code to $200/main
+        jmp   LoadAuxIndexedFile    ; load decompression code to $200/aux
@params  !word $200
         !word kDecrunchRecord
         ldx   auxreq
         sta   READMAINMEM,x         ; X = 0 or 1, so this will be READMAINMEM or READAUXMEM
         sta   WRITEMAINMEM,x        ; X = 0 or 1, so this will be WRITEMAINMEM or WRITEAUXMEM
         beq   +
         lda   #$60
         sta   DECRUNCH_EXIT_ON_KEYPRESS
+        jsr   DECRUNCH
         sta   READMAINMEM
         sta   WRITEMAINMEM
         jsr   SwitchToBank2
         lda   #0
         sta   auxreq
         jmp   DisableAcceleratorAndSwitchToBank1

;------------------------------------------------------------------------------
; DecompressDHGR
;
; in:    compressed DHGR loaded to $3FF8+/aux
;        file size in sizelo2/sizehi2 as set by ProRWTS2
; out:   decompressed HGR at $4000..$5FFF/main and $4000..$5FFF/aux
;        $6000..$7FFF/aux clobbered by decompressor
;        $00A7..$00A8 clobbered by decompressor
;        $00AE..$00AF clobbered by decompressor
;        $00FC..$00FF clobbered by decompressor
;        $0200..$03CB clobbered by decompressor
;------------------------------------------------------------------------------
DecompressDHGR
         jsr   DecompressHGR
         sta   WRITEMAINMEM
         lda   #$60
         sta   PageFrom+2
         lda   #$40
         jmp   CopyHGR               ; switches back to READMAINMEM on exit
